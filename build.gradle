plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"
    id "io.freefair.lombok" version "5.3.0"
    id "com.palantir.docker" version "0.26.0"
}

apply from: 'common-build.gradle'

project.version = 'v2.1'

// TestCanvasManager tests limits and uses a lot of memory
test {
    minHeapSize = "512m"
    maxHeapSize = "1024m"
}

bootJar {
    // Remove version number from bootJar
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

docker {
    name "spring-websockets-example:${project.version}"
    tag "ecrTag", "953430798342.dkr.ecr.eu-west-2.amazonaws.com/spring-websockets-example:${project.version}"
    tag "ecrLatestTag", "953430798342.dkr.ecr.eu-west-2.amazonaws.com/spring-websockets-example:latest"
    dockerfile project.file('Dockerfile')
    files tasks.bootJar.outputs
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework:spring-websocket'
    implementation 'org.springframework:spring-messaging'

    // Generally useful java things
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'

    // Test utilities
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.4.0'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation group: 'org.springframework', name: 'spring-test'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

sourceCompatibility = 11
targetCompatibility = 11
